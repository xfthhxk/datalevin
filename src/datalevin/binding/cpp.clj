;; ;; Copyright (c) Huahai Yang. All rights reserved.
;; The use and distribution terms for this software are covered by the
;; Eclipse Public License 2.0 (https://opensource.org/license/epl-2-0)
;; which can be found in the file LICENSE at the root of this distribution.
;; By using this software in any fashion, you are agreeing to be bound by
;; the terms of this license.
;; You must not remove this notice, or any other, from this software.
;;
(ns ^:no-doc datalevin.binding.cpp
  "Native binding to LMDB using JavaCPP"
  (:refer-clojure :exclude [sync])
  (:require
   [clojure.java.io :as io]
   [clojure.string :as s]
   [datalevin.bits :as b]
   [datalevin.util :as u :refer [raise]]
   [datalevin.constants :as c]
   [datalevin.compress :as cp]
   [datalevin.buffer :as bf]
   [datalevin.async :as a]
   [datalevin.migrate :as m]
   [datalevin.validate :as vld]
   [datalevin.scan :as scan]
   [datalevin.interface :as i
    :refer [IList ILMDB IAdmin open-dbi close-kv env-dir close-vecs
            transact-kv get-range stat key-compressor
            val-compressor set-max-val-size max-val-size
            set-key-compressor set-val-compressor
            bf-compress bf-uncompress]]
   [datalevin.lmdb :as l
    :refer [open-kv IBuffer IRange IRtx IDB IKV IWriting ICompress
            IListRandKeyValIterable IListRandKeyValIterator]])
  (:import
   [datalevin.dtlvnative DTLV DTLV$MDB_envinfo DTLV$MDB_stat DTLV$dtlv_key_iter
    DTLV$dtlv_list_iter DTLV$dtlv_list_key_range_full_val_iter
    DTLV$dtlv_list_rank_sample_iter DTLV$dtlv_list_val_full_iter DTLV$MDB_val
    DTLV$dtlv_key_rank_sample_iter]
   [datalevin.cpp BufVal Env Txn Dbi Cursor Stat Info Util Util$MapFullException
    UnsafeAccess]
   [datalevin.lmdb RangeContext KVTxData]
   [datalevin.async IAsyncWork]
   [datalevin.utl BitOps]
   [java.util.concurrent TimeUnit ScheduledExecutorService ScheduledFuture
    ConcurrentHashMap]
   [java.util.concurrent.atomic AtomicBoolean]
   [java.lang AutoCloseable]
   [java.io File]
   [java.util Iterator HashMap ArrayDeque Map$Entry]
   [java.util.function Supplier]
   [java.nio BufferOverflowException ByteBuffer]
   [org.bytedeco.javacpp SizeTPointer LongPointer]
   [clojure.lang IObj]))

(defn- version-file
  [^File dir]
  (io/file dir c/version-file-name))

(defn- write-version-file
  [^File dir version]
  (when (and version (not (s/blank? ^String version)))
    (spit (version-file dir) version)
    version))

(defn- read-version-file
  [^File dir]
  (try
    (let [^File f (version-file dir)]
      (when (.exists f)
        (some-> (slurp f) s/trim not-empty)))
    (catch Exception e
      (raise "Unable to read VERSION file"
             {:msg (.getMessage e)}))))

(def ^:dynamic *before-write-commit-fn*
  nil)

(def ^:private duplicate-local-open-msg
  "Please do not open multiple LMDB connections to the same DB
           in the same process. Instead, a LMDB connection should be held onto
           and managed like a stateful resource. Refer to the documentation of
           `datalevin.core/open-kv` for more details.")

(defn- run-before-write-commit!
  [context]
  (when-let [f *before-write-commit-fn*]
    (f context)))

(defprotocol IPool
  (pool-add [_ x])
  (pool-take [_]))

(defprotocol ICloseableResource
  (close-resource! [_]))

(deftype Pool [^ThreadLocal que]
  IPool
  (pool-add [_ x] (.add ^ArrayDeque (.get que) x))
  (pool-take [_] (.poll ^ArrayDeque (.get que))))

(defn- new-pools
  []
  (Pool. (ThreadLocal/withInitial
          (reify Supplier
            (get [_] (ArrayDeque.))))))

(defn- new-bufval [size] (BufVal. size))

(defn- close-txn-quiet!
  [^Txn txn]
  (when txn
    (try
      (.close txn)
      (catch Exception _))))

(defn- close-bufval-quiet!
  [^BufVal bufval]
  (when bufval
    (try
      (.close bufval)
      (catch Throwable _))))

(defn- clean-buffer-quiet!
  [^ByteBuffer buffer]
  (when buffer
    (try
      (UnsafeAccess/clean buffer)
      (catch Throwable _))))

(defn- close-cursor-quiet!
  [^Cursor cur]
  (when cur
    (try
      (.close cur)
      (catch Throwable _))))

(defn- bufval-open?
  [^BufVal bufval]
  (try
    (some? (.ptr bufval))
    (catch Throwable _ false)))

(defn- cursor-open?
  [^Cursor cur]
  (and (bufval-open? (.key cur))
       (bufval-open? (.val cur))))

(defn- reusable-cursor
  [^Pool curs ^Txn txn]
  (loop []
    (when-let [^Cursor cur (pool-take curs)]
      (if-not (cursor-open? cur)
        (do
          (close-cursor-quiet! cur)
          (recur))
        (let [renewed (try
                        (.renew cur txn)
                        cur
                        (catch Throwable _
                          (close-cursor-quiet! cur)
                          nil))]
          (if renewed
            renewed
            (recur)))))))

(defn- flag-value
  "flag key to int value, cover all flags"
  [k]
  (case k
    :fixedmap DTLV/MDB_FIXEDMAP
    :nosubdir DTLV/MDB_NOSUBDIR
    :rdonly-env DTLV/MDB_RDONLY
    :writemap DTLV/MDB_WRITEMAP
    :nometasync DTLV/MDB_NOMETASYNC
    :nosync DTLV/MDB_NOSYNC
    :mapasync DTLV/MDB_MAPASYNC
    :notls DTLV/MDB_NOTLS
    :nolock DTLV/MDB_NOLOCK
    :nordahead DTLV/MDB_NORDAHEAD
    :nomeminit DTLV/MDB_NOMEMINIT
    :inmemory DTLV/MDB_INMEMORY

    :cp-compact DTLV/MDB_CP_COMPACT

    :reversekey DTLV/MDB_REVERSEKEY
    :dupsort DTLV/MDB_DUPSORT
    :integerkey DTLV/MDB_INTEGERKEY
    :dupfixed DTLV/MDB_DUPFIXED
    :integerdup DTLV/MDB_INTEGERDUP
    :reversedup DTLV/MDB_REVERSEDUP
    :create DTLV/MDB_CREATE
    :prefix-compression DTLV/MDB_PREFIX_COMPRESSION
    :counted DTLV/MDB_COUNTED

    :nooverwrite DTLV/MDB_NOOVERWRITE
    :nodupdata DTLV/MDB_NODUPDATA
    :current DTLV/MDB_CURRENT
    :reserve DTLV/MDB_RESERVE
    :append DTLV/MDB_APPEND
    :appenddup DTLV/MDB_APPENDDUP
    :multiple DTLV/MDB_MULTIPLE

    :rdonly-txn DTLV/MDB_RDONLY))

(defn- kv-flags
  [flags]
  (if (seq flags)
    (reduce (fn [r f] (bit-or ^int r ^int f))
            0 (mapv flag-value flags))
    (int 0)))

(defonce env-flag-map
  {0x01 :fixedmap
   0x4000 :nosubdir
   0x10000 :nosync
   0x20000 :rdonly-env
   0x40000 :nometasync
   0x80000 :writemap
   0x100000 :mapasync
   0x200000 :notls
   0x400000 :nolock
   0x800000 :nordahead
   0x1000000 :nomeminit})

(defn- env-flag-keys
  [v]
  (reduce-kv
   (fn [s i k]
     (if (not= 0 (bit-and ^int i ^int v))
       (conj s k)
       s))
   #{} env-flag-map))

(defn- put-bufval
  [^BufVal vp k kt compressor ^ByteBuffer cbf]
  (when-some [x k]
    (let [^ByteBuffer bf (.inBuf vp)]
      (.clear bf)
      (if compressor
        (do (b/put-buffer (.clear cbf) x kt)
            (bf-compress compressor (.flip cbf) bf))
        (b/put-buffer bf x kt))
      (.flip bf)
      (.reset vp))))

(deftype Rtx [^:unsynchronized-mutable lmdb
              ^Txn txn
              depth
              ^BufVal kp
              ^BufVal vp
              ^BufVal start-kp
              ^BufVal stop-kp
              ^BufVal start-vp
              ^BufVal stop-vp
              ^:unsynchronized-mutable ^ByteBuffer k-comp-bf
              ^:volatile-mutable ^ByteBuffer v-comp-bf
              aborted?
              ^AtomicBoolean closed?
              ^boolean owns-buffers?]

  ICompress
  (key-bf [_] (.clear k-comp-bf))
  (val-bf [_] (.clear v-comp-bf))

  IBuffer
  (put-key [_ x t]
    (try
      (put-bufval kp x t (key-compressor lmdb) k-comp-bf)
      (catch BufferOverflowException _
        (raise "Key cannot be larger than 511 bytes." {:input x}))
      (catch Exception e
        (raise "Error putting read-only transaction key buffer: "
               e {:value x :type t}))))
  (put-val [_ _ _]
    (raise "put-val not allowed for read only txn buffer" {}))

  IRange
  (range-info [_ range-type k1 k2 kt]
    (put-bufval start-kp k1 kt (key-compressor lmdb) k-comp-bf)
    (put-bufval stop-kp k2 kt (key-compressor lmdb) k-comp-bf)
    (l/range-table range-type start-kp stop-kp))

  (list-range-info [_ k-range-type k1 k2 kt v-range-type v1 v2 vt]
    (put-bufval start-kp k1 kt (key-compressor lmdb) k-comp-bf)
    (put-bufval stop-kp k2 kt (key-compressor lmdb) k-comp-bf)
    (put-bufval start-vp v1 vt (val-compressor lmdb) v-comp-bf)
    (put-bufval stop-vp v2 vt (val-compressor lmdb) v-comp-bf)
    [(l/range-table k-range-type start-kp stop-kp)
     (l/range-table v-range-type start-vp stop-vp)])

  IRtx
  (read-only? [_] (.isReadOnly txn))
  (reset [this]
    (vswap! depth u/long-dec)
    (when (zero? ^long @depth)
      (.reset txn))
    this)

  (renew [this]
    (when (zero? ^long @depth)
      (.renew txn))
    (vswap! depth u/long-inc)
    this)

  AutoCloseable
  (close [_]
    (when (.compareAndSet closed? false true)
      (let [txn*       txn
            kp*        kp
            vp*        vp
            start-kp*  start-kp
            stop-kp*   stop-kp
            start-vp*  start-vp
            stop-vp*   stop-vp
            k-comp-bf* k-comp-bf
            v-comp-bf* v-comp-bf]
        (set! lmdb nil)
        (set! k-comp-bf nil)
        (set! v-comp-bf nil)
        (close-txn-quiet! txn*)
        (when owns-buffers?
          (close-bufval-quiet! kp*)
          (close-bufval-quiet! vp*)
          (close-bufval-quiet! start-kp*)
          (close-bufval-quiet! stop-kp*)
          (close-bufval-quiet! start-vp*)
          (close-bufval-quiet! stop-vp*)
          (clean-buffer-quiet! k-comp-bf*)
          (clean-buffer-quiet! v-comp-bf*))))
    nil))

(defn- v-bf
  [^BufVal vp lmdb rtx]
  (let [bf (.outBuf vp)]
    (if-let [compressor (val-compressor lmdb)]
      (let [^ByteBuffer cbf (l/val-bf rtx)]
        (bf-uncompress compressor bf cbf)
        (.flip cbf))
      bf)))

(deftype KV [^BufVal kp ^BufVal vp lmdb rtx]
  IKV
  (k [_]
    (let [bf (.outBuf kp)]
      (if-let [compressor (key-compressor lmdb)]
        (let [^ByteBuffer cbf (l/key-bf rtx)]
          (bf-uncompress compressor bf cbf)
          (.flip cbf))
        bf)))

  (v [_] (v-bf vp lmdb rtx)))

(defn- stat-map [^Stat stat]
  (let [^DTLV$MDB_stat s (.get stat)]
    {:psize (.ms_psize s)
     :depth (.ms_depth s)
     :branch-pages (.ms_branch_pages s)
     :leaf-pages (.ms_leaf_pages s)
     :overflow-pages (.ms_overflow_pages s)
     :entries (.ms_entries s)}))

(declare ->KeyIterable ->KeySampleIterable ->ListIterable
         ->ListFullValIterable ->ListSampleIterable
         ->ListKeyRangeFullValIterable)

(defn- val-size
  [x]
  (let [^long val-size (b/measure-size x)]
    (if (< Integer/MAX_VALUE val-size)
      (raise "Value size is too large" {:size val-size})
      (let [try-size (* ^long c/+buffer-grow-factor+ val-size)]
        (if (< Integer/MAX_VALUE try-size)
          val-size
          try-size)))))

(deftype DBI [lmdb
              ^Dbi db
              ^Pool curs
              ^BufVal kp
              ^:volatile-mutable ^BufVal vp
              ^ByteBuffer k-comp-bf
              ^:volatile-mutable ^ByteBuffer v-comp-bf
              ^boolean dupsort?
              ^boolean counted?
              ^boolean validate-data?]
  IBuffer
  (put-key [this x t]
    (try
      (put-bufval kp x t (key-compressor lmdb) k-comp-bf)
      (catch BufferOverflowException _
        (raise "Key cannot be larger than 511 bytes." {:input x}))
      (catch Exception e
        (raise "Error putting r/w key buffer of "
               (.dbi-name this) ": " e {:value x :type t}))))
  (put-val [this x t]
    (try
      (put-bufval vp x t (val-compressor lmdb) v-comp-bf)
      (catch BufferOverflowException _
        (let [size          (val-size x)
              old-vp        vp
              old-v-comp-bf v-comp-bf]
          (set! vp (new-bufval size))
          (set! v-comp-bf (bf/allocate-buffer size))
          (close-bufval-quiet! old-vp)
          (clean-buffer-quiet! old-v-comp-bf)
          (set-max-val-size lmdb size)
          (put-bufval vp x t (val-compressor lmdb) v-comp-bf)))
      (catch Exception e
        (raise "Error putting r/w value buffer of "
               (.dbi-name this) ": " e {:value x :type t}))))

  IDB
  (dbi [_] db)
  (dbi-name [_] (.getName db))
  (put [_ txn flags] (.put db txn kp vp (kv-flags flags)))
  (put [this txn] (.put this txn nil))
  (del [_ txn all?] (if all? (.del db txn kp nil) (.del db txn kp vp)))
  (del [this txn] (.del this txn true))
  (get-kv [_ rtx]
    (let [^BufVal kp (.-kp ^Rtx rtx)
          ^BufVal vp (.-vp ^Rtx rtx)
          rc (DTLV/mdb_get (.get ^Txn (.-txn ^Rtx rtx))
                           (.get db) (.ptr kp) (.ptr vp))]
      (Util/checkRc ^int rc)
      (when-not (= rc DTLV/MDB_NOTFOUND)
        (.outBuf vp))))
  (get-key-rank [_ rtx]
    (let [^BufVal kp (.-kp ^Rtx rtx)
          ^LongPointer rp (LongPointer. 1)
          rc (DTLV/mdb_get_key_rank (.get ^Txn (.-txn ^Rtx rtx))
                                    (.get db) (.ptr kp) nil rp)]
      (Util/checkRc ^int rc)
      (when-not (= rc DTLV/MDB_NOTFOUND)
        (.get rp))))
  (get-key-by-rank [_ rtx rank]
    (let [^BufVal kp (.-kp ^Rtx rtx)
          ^BufVal vp (.-vp ^Rtx rtx)
          rc (DTLV/mdb_get_rank (.get ^Txn (.-txn ^Rtx rtx))
                                (.get db) (long rank) (.ptr kp) (.ptr vp))]
      (Util/checkRc ^int rc)
      (when-not (= rc DTLV/MDB_NOTFOUND)
        [(.outBuf kp) (.outBuf vp)])))
  (iterate-key [this rtx cur [range-type k1 k2] k-type]
    (let [ctx (l/range-info rtx range-type k1 k2 k-type)]
      (->KeyIterable lmdb this cur rtx ctx)))
  (iterate-key-sample [this rtx cur indices [range-type k1 k2] k-type]
    (let [ctx (l/range-info rtx range-type k1 k2 k-type)]
      (->KeySampleIterable lmdb this indices cur rtx ctx)))
  (iterate-list [this rtx cur [k-range-type k1 k2] k-type
                 [v-range-type v1 v2] v-type]
    (let [ctx (l/list-range-info rtx k-range-type k1 k2 k-type
                                 v-range-type v1 v2 v-type)]
      (->ListIterable lmdb this cur rtx ctx)))
  (iterate-list-sample [this rtx cur indices [k-range-type k1 k2] k-type]
    (let [ctx (l/range-info rtx k-range-type k1 k2 k-type)]
      (->ListSampleIterable lmdb this indices cur rtx ctx)))
  (iterate-list-key-range-val-full [this rtx cur [range-type k1 k2] k-type]
    (let [ctx (l/range-info rtx range-type k1 k2 k-type)]
      (->ListKeyRangeFullValIterable lmdb this cur rtx ctx)))
  (iterate-list-val-full [this rtx cur]
    (->ListFullValIterable lmdb this cur rtx))
  (iterate-kv [this rtx cur k-range k-type v-type]
    (if dupsort?
      (let [range-type (first k-range)]
        (if (and (keyword? range-type)
                 (s/ends-with? (name range-type) "-back"))
          (.iterate-list this rtx cur k-range k-type [:all] v-type)
          (.iterate-list-key-range-val-full this rtx cur k-range k-type)))
      (.iterate-key this rtx cur k-range k-type)))
  (get-cursor [_ rtx]
    (let [^Rtx rtx rtx
          ^Txn txn (.-txn rtx)]
      (or (when (.isReadOnly txn)
            (reusable-cursor curs txn))
          (Cursor/create txn db (.-kp rtx) (.-vp rtx)))))
  (cursor-count [_ cur] (.count ^Cursor cur))
  (close-cursor [_ cur] (.close ^Cursor cur))
  (return-cursor [_ cur] (pool-add curs cur))

  ICloseableResource
  (close-resource! [_]
    (close-bufval-quiet! kp)
    (close-bufval-quiet! vp)
    (clean-buffer-quiet! k-comp-bf)
    (clean-buffer-quiet! v-comp-bf)
    (try
      (.close db)
      (catch Throwable _))
    nil))

(defn- close-dbi-quiet!
  [^DBI dbi]
  (when dbi
    (try
      (close-resource! dbi)
      (catch Throwable _))))

(defn- dtlv-bool [x] (if x DTLV/DTLV_TRUE DTLV/DTLV_FALSE))

(defn- dtlv-val ^DTLV$MDB_val [x] (when x (.ptr ^BufVal x)))

(defn- dtlv-rc [x]
  (condp = x
    DTLV/DTLV_TRUE true
    DTLV/DTLV_FALSE false
    (u/raise "Native iterator returns error code" x {})))

(defn- dtlv-c [^long x]
  (if (< x 0)
    (u/raise "Native counter returns error code" x {})
    x))

(deftype KeyIterable [lmdb
                      ^DBI db
                      ^Cursor cur
                      ^Rtx rtx
                      ^RangeContext ctx]
  Iterable
  (iterator [_]
    (let [forward? (dtlv-bool (.-forward? ctx))
          include-start? (dtlv-bool (.-include-start? ctx))
          include-stop? (dtlv-bool (.-include-stop? ctx))
          sk (dtlv-val (.-start-bf ctx))
          ek (dtlv-val (.-stop-bf ctx))
          k (.key cur)
          v (.val cur)
          iter (DTLV$dtlv_key_iter.)]
      (Util/checkRc
       (DTLV/dtlv_key_iter_create
        iter (.ptr cur) (.ptr k) (.ptr v)
        ^int forward? ^int include-start? ^int include-stop? sk ek))
      (reify
        Iterator
        (hasNext [_] (dtlv-rc (DTLV/dtlv_key_iter_has_next iter)))
        (next [_] (KV. k v lmdb rtx))

        AutoCloseable
        (close [_] (DTLV/dtlv_key_iter_destroy iter))))))

(deftype KeySampleIterable [lmdb
                            ^DBI db
                            ^longs indices
                            ^Cursor cur
                            ^Rtx rtx
                            ^RangeContext ctx]
  Iterable
  (iterator [_]
    (let [sk (dtlv-val (.-start-bf ctx))
          ek (dtlv-val (.-stop-bf ctx))
          k (.key cur)
          v (.val cur)
          iter (DTLV$dtlv_key_rank_sample_iter.)
          samples (alength indices)
          sizets (SizeTPointer. samples)]
      (dotimes [i samples] (.put sizets i (aget indices i)))
      (Util/checkRc
       (DTLV/dtlv_key_rank_sample_iter_create
        ^DTLV$dtlv_key_rank_sample_iter iter
        sizets samples (.ptr cur) (.ptr k) (.ptr v) sk ek))
      (reify
        Iterator
        (hasNext [_] (dtlv-rc (DTLV/dtlv_key_rank_sample_iter_has_next iter)))
        (next [_] (KV. k v lmdb rtx))

        AutoCloseable
        (close [_] (DTLV/dtlv_key_rank_sample_iter_destroy iter))))))

(deftype ListIterable [lmdb
                       ^DBI db
                       ^Cursor cur
                       ^Rtx rtx
                       ctx]
  Iterable
  (iterator [_]
    (let [[^RangeContext kctx ^RangeContext vctx] ctx

          forward-key? (dtlv-bool (.-forward? kctx))
          include-start-key? (dtlv-bool (.-include-start? kctx))
          include-stop-key? (dtlv-bool (.-include-stop? kctx))
          sk (dtlv-val (.-start-bf kctx))
          ek (dtlv-val (.-stop-bf kctx))
          forward-val? (dtlv-bool (.-forward? vctx))
          include-start-val? (dtlv-bool (.-include-start? vctx))
          include-stop-val? (dtlv-bool (.-include-stop? vctx))
          sv (dtlv-val (.-start-bf vctx))
          ev (dtlv-val (.-stop-bf vctx))
          k (.key cur)
          v (.val cur)
          iter (DTLV$dtlv_list_iter.)]
      (Util/checkRc
       (DTLV/dtlv_list_iter_create
        iter (.ptr cur) (.ptr k) (.ptr v)
        ^int forward-key? ^int include-start-key? ^int include-stop-key? sk ek
        ^int forward-val? ^int include-start-val? ^int include-stop-val?
        sv ev))
      (reify
        Iterator
        (hasNext [_] (dtlv-rc (DTLV/dtlv_list_iter_has_next iter)))
        (next [_] (KV. k v lmdb rtx))

        AutoCloseable
        (close [_] (DTLV/dtlv_list_iter_destroy iter))))))

(deftype ListSampleIterable [lmdb
                             ^DBI db
                             ^longs indices
                             ^Cursor cur
                             ^Rtx rtx
                             ^RangeContext ctx]
  Iterable
  (iterator [_]
    (let [sk      (dtlv-val (.-start-bf ctx))
          ek      (dtlv-val (.-stop-bf ctx))
          k       (.key cur)
          v       (.val cur)
          iter    (DTLV$dtlv_list_rank_sample_iter.)
          samples (alength indices)
          sizets  (SizeTPointer. samples)]
      (dotimes [i samples] (.put sizets i (aget indices i)))
      (Util/checkRc
        (DTLV/dtlv_list_rank_sample_iter_create
          ^DTLV$dtlv_list_rank_sample_iter iter
          sizets samples (.ptr cur) (.ptr k) (.ptr v) sk ek))
      (reify
        Iterator
        (hasNext [_] (dtlv-rc (DTLV/dtlv_list_rank_sample_iter_has_next iter)))
        (next [_] (KV. k v lmdb rtx))

        AutoCloseable
        (close [_] (DTLV/dtlv_list_rank_sample_iter_destroy iter))))))

(deftype ListKeyRangeFullValIterable [lmdb
                                      ^DBI db
                                      ^Cursor cur
                                      ^Rtx rtx
                                      ^RangeContext ctx]
  Iterable
  (iterator [_]
    (let [include-start? (dtlv-bool (.-include-start? ctx))
          include-stop?  (dtlv-bool (.-include-stop? ctx))
          sk             (dtlv-val (.-start-bf ctx))
          ek             (dtlv-val (.-stop-bf ctx))
          k              (.key cur)
          v              (.val cur)
          iter           (DTLV$dtlv_list_key_range_full_val_iter.)]
      (Util/checkRc
       (DTLV/dtlv_list_key_range_full_val_iter_create
        iter (.ptr cur) (.ptr k) (.ptr v)
        ^int include-start? ^int include-stop? sk ek))
      (reify
        Iterator
        (hasNext [_]
          (dtlv-rc (DTLV/dtlv_list_key_range_full_val_iter_has_next iter)))
        (next [_] (KV. k v lmdb rtx))

        AutoCloseable
        (close [_] (DTLV/dtlv_list_key_range_full_val_iter_destroy iter))))))

(deftype ListFullValIterable [lmdb
                              ^DBI db
                              ^Cursor cur
                              ^Rtx rtx]
  IListRandKeyValIterable
  (val-iterator [_]
    (let [^BufVal k (.key cur)
          ^BufVal v (.val cur)
          iter (DTLV$dtlv_list_val_full_iter.)]
      (Util/checkRc
       (DTLV/dtlv_list_val_full_iter_create iter (.ptr cur) (.ptr k) (.ptr v)))
      (reify
        IListRandKeyValIterator
        (seek-key [_ x t]
          (l/put-key rtx x t)
          (dtlv-rc
           (DTLV/dtlv_list_val_full_iter_seek iter (.ptr ^BufVal (.-kp rtx)))))
        (has-next-val [_]
          (dtlv-rc (DTLV/dtlv_list_val_full_iter_has_next iter)))
        (next-val [_] (v-bf v lmdb rtx))

        AutoCloseable
        (close [_] (DTLV/dtlv_list_val_full_iter_destroy iter))))))

(defn- put-tx
  [^DBI dbi txn ^KVTxData tx]
  (case (.-op tx)
    :put      (do (.put-key dbi (.-k tx) (.-kt tx))
                  (.put-val dbi (.-v tx) (.-vt tx))
                  (if-let [f (.-flags tx)]
                    (.put dbi txn f)
                    (.put dbi txn)))
    :del      (do (.put-key dbi (.-k tx) (.-kt tx))
                  (.del dbi txn))
    :put-list (let [vs (.-v tx)]
                (.put-key dbi (.-k tx) (.-kt tx))
                (doseq [v vs]
                  (.put-val dbi v (.-vt tx))
                  (.put dbi txn)))
    :del-list (let [vs         (.-v tx)
                    vt         (.-vt tx)
                    ^BufVal kp (.-kp dbi)]
                (.put-key dbi (.-k tx) (.-kt tx))
                (doseq [v vs]
                  (.put-val dbi v vt)
                  (.del dbi txn false)
                  ;; mdb_del may mutate the native key if value is missing
                  (.reset kp)))))

(defn- transact1*
  [txs ^DBI dbi txn kt vt]
  (let [validate? (.-validate-data? dbi)]
    (doseq [t txs]
      (let [tx (l/->kv-tx-data t kt vt)]
        (vld/validate-kv-tx-data tx validate?)
        (put-tx dbi txn tx)))))

(defn- transact*
  [txs ^HashMap dbis txn]
  (doseq [t txs]
    (let [^KVTxData tx (l/->kv-tx-data t)
          dbi-name (.-dbi-name tx)
          ^DBI dbi (or (.get dbis dbi-name)
                       (raise dbi-name " is not open" {}))
          validate? (.-validate-data? dbi)]
      (vld/validate-kv-tx-data tx validate?)
      (put-tx dbi txn tx))))

(defn- transact-prepared-ops*
  [ops txn]
  (doseq [op ops]
    (let [^DBI dbi (nth op 0)
          ^KVTxData tx (nth op 1)]
      (put-tx dbi txn tx))))

(defn- prepare-kvtx-ops
  [txs ^HashMap dbis dbi-name kt vt]
  (let [out (transient [])]
    (if dbi-name
      (let [^DBI dbi (or (.get dbis dbi-name)
                         (raise dbi-name " is not open" {}))
            validate? (.-validate-data? dbi)]
        (doseq [t txs]
          (let [^KVTxData tx (l/->kv-tx-data t kt vt)]
            (vld/validate-kv-tx-data tx validate?)
            (conj! out [dbi tx]))))
      (doseq [t txs]
        (let [^KVTxData tx (l/->kv-tx-data t)
              dbi-name* (.-dbi-name tx)
              ^DBI dbi (or (.get dbis dbi-name*)
                           (raise dbi-name* " is not open" {}))
              validate? (.-validate-data? dbi)]
          (vld/validate-kv-tx-data tx validate?)
          (conj! out [dbi tx]))))
    (persistent! out)))

(defn- kv-tx->row
  [^KVTxData tx]
  (let [op   (.-op tx)
        base (if (= op :del)
               [op (.-dbi-name tx) (.-k tx) (.-kt tx)]
               [op (.-dbi-name tx) (.-k tx) (.-v tx)
                (.-kt tx) (.-vt tx) (.-flags tx)])
        minc (if (= op :del) 3 4)]
    (loop [row base]
      (if (and (> (count row) minc) (nil? (peek row)))
        (recur (pop row))
        row))))

(def ^:private max-val-size-row-prefix
  [:put c/kv-info :max-val-size])

(defn- max-val-size-row
  [size]
  (conj max-val-size-row-prefix size :data :data))

(defn- maybe-apply-max-val-size-op!
  [info ^HashMap dbis txn rows]
  (if (:max-val-size-changed? @info)
    (let [row (max-val-size-row (:max-val-size @info))]
      (transact* [row] dbis txn)
      (vswap! info assoc :max-val-size-changed? false)
      (conj rows row))
    rows))

(defn- list-count*
  [^Rtx rtx ^Cursor cur k kt]
  (.put-key rtx k kt)
  (dtlv-c (DTLV/dtlv_list_val_count
           (.ptr cur) (.ptr ^BufVal (.-kp rtx)) (.ptr ^BufVal (.-vp rtx)))))

(defn- in-list?*
  [^Rtx rtx ^Cursor cur k kt v vt]
  (l/list-range-info rtx :at-least k nil kt :at-least v nil vt)
  (.get cur ^BufVal (.-start-kp rtx) ^BufVal (.-start-vp rtx)
        DTLV/MDB_GET_BOTH))

(defn- near-list*
  [^Rtx rtx ^Cursor cur k kt v vt]
  (l/list-range-info rtx :at-least k nil kt :at-least v nil vt)
  (when (.get cur ^BufVal (.-start-kp rtx) ^BufVal (.-start-vp rtx)
              DTLV/MDB_GET_BOTH_RANGE)
    (.outBuf (.val cur))))

(declare ->CppLMDB)

(defn- up-db-size [^Env env]
  (let [^Info info (Info/create env)]
    (.setMapSize env (* ^long c/+buffer-grow-factor+
                        (.me_mapsize ^DTLV$MDB_envinfo (.get info))))
    (.close info)))

(defn- close-rtx-quiet!
  [^Rtx rtx]
  (when rtx
    (try
      (.close ^AutoCloseable rtx)
      (catch Throwable _))))

(defn- discard-thread-reader!
  [^ThreadLocal tl-reader ^ConcurrentHashMap reader-registry
   ^Thread thread ^Rtx rtx]
  (.remove tl-reader)
  (.remove reader-registry thread rtx)
  (close-rtx-quiet! rtx)
  nil)

(defn- sweep-dead-reader-rtxs!
  [^ConcurrentHashMap reader-registry]
  (let [closed (volatile! 0)]
    (doseq [^Map$Entry entry (.entrySet reader-registry)]
      (let [^Thread thread (.getKey entry)
            ^Rtx rtx      (.getValue entry)]
        (when-not (.isAlive thread)
          (when (.remove reader-registry thread rtx)
            (close-rtx-quiet! rtx)
            (vswap! closed u/long-inc)))))
    @closed))

(defn- close-reader-rtxs!
  [^ConcurrentHashMap reader-registry]
  (doseq [^Map$Entry entry (.entrySet reader-registry)]
    (let [^Thread thread (.getKey entry)
          ^Rtx rtx      (.getValue entry)]
      (when (.remove reader-registry thread rtx)
        (close-rtx-quiet! rtx)))))

(defn- reusable-reader-rtx
  [this ^ThreadLocal tl-reader ^ConcurrentHashMap reader-registry]
  (let [thread (Thread/currentThread)]
    (when-not (.isVirtual thread)
      (when-let [^Rtx rtx (.get tl-reader)]
        (if (<= (long (max-val-size this))
                ^int (.capacity ^ByteBuffer (l/val-bf rtx)))
          (try
            (.renew rtx)
            (catch Exception _
              ;; Storage faults can leave a cached reader txn stale even though
              ;; the LMDB env itself remains valid. Drop the stale reader and let
              ;; the caller open a fresh one instead of surfacing a misleading
              ;; "multiple connections" error for subsequent reads.
              (discard-thread-reader! tl-reader reader-registry thread rtx)))
          (discard-thread-reader! tl-reader reader-registry thread rtx))))))

(defn- fresh-reader-rtx
  [this ^Env env ^ThreadLocal tl-reader ^ConcurrentHashMap reader-registry]
  (let [thread        (Thread/currentThread)
        max-val-size* (long (max-val-size this))
        rtx (Rtx. this
                  (Txn/createReadOnly env)
                  (volatile! 1)
                  (new-bufval c/+max-key-size+)
                  (new-bufval 0)
                  (new-bufval c/+max-key-size+)
                  (new-bufval c/+max-key-size+)
                  (new-bufval c/+max-key-size+)
                  (new-bufval c/+max-key-size+)
                  (bf/allocate-buffer c/+max-key-size+)
                  (bf/allocate-buffer max-val-size*)
                  (volatile! false)
                  (AtomicBoolean.)
                  true)]
    (.set tl-reader rtx)
    (when-not (.isVirtual thread)
      (when-let [^Rtx old (.put reader-registry thread rtx)]
        (when-not (identical? old rtx)
          (close-rtx-quiet! old))))
    rtx))

(defn- sync-key* [dir] (->> dir hash (str "lmdb-sync-") keyword))

(def sync-key (memoize sync-key*))

(deftype AsyncSync [dir ^Env env]
  IAsyncWork
  (work-key [_] (sync-key dir))
  (do-work [_] (.sync env 1))
  (combine [_] first)
  (callback [_] nil))

(defn- start-scheduled-sync
  [scheduled-sync dir ^Env env]
  (let [scheduler ^ScheduledExecutorService (u/get-scheduler)
        fut (.scheduleWithFixedDelay
             scheduler
             ^Runnable #(let [exe (a/get-executor)]
                          (when (a/running? exe)
                            (a/exec exe (AsyncSync. dir env))))
             ^long (rand-int c/lmdb-sync-interval)
             ^long c/lmdb-sync-interval
             TimeUnit/SECONDS)]
    (vreset! scheduled-sync fut)))

(defonce ^:private shutdown-hooks (atom {}))
(defonce ^:private active-local-kv-handles (atom #{}))
(defonce ^:private open-local-kv-handles (atom {}))

(defn- canonical-dir-key
  [^File dir-file]
  (.getCanonicalPath dir-file))

(defn- local-kv-handle-key
  [^File dir-file flags]
  (when-not (some #{:inmemory} flags)
    (canonical-dir-key dir-file)))

(defn- reserve-local-kv-handle!
  [^File dir-file flags]
  (when-let [dir-key (local-kv-handle-key dir-file flags)]
    (let [[before _]
          (swap-vals! active-local-kv-handles
                      #(if (contains? % dir-key) % (conj % dir-key)))]
      (when (contains? before dir-key)
        (raise duplicate-local-open-msg
               {:dir dir-key
                :type :lmdb/duplicate-open}))
      dir-key)))

(defn- register-local-kv-handle!
  [dir-key lmdb]
  (when dir-key
    (swap! open-local-kv-handles assoc dir-key lmdb))
  lmdb)

(defn open-local-kv-handle
  [dir]
  (when-let [dir-key (some-> dir u/file canonical-dir-key)]
    (locking open-local-kv-handles
      (when-let [lmdb (get @open-local-kv-handles dir-key)]
        (if (i/closed-kv? lmdb)
          (do
            (swap! open-local-kv-handles dissoc dir-key)
            (swap! active-local-kv-handles disj dir-key)
            nil)
          lmdb)))))

(defn- release-local-kv-handle!
  [dir-key]
  (when dir-key
    (swap! active-local-kv-handles disj dir-key)
    (swap! open-local-kv-handles dissoc dir-key))
  nil)

(defn- register-shutdown-hook!
  [dir ^Thread hook]
  (.addShutdownHook (Runtime/getRuntime) hook)
  (swap! shutdown-hooks assoc dir hook)
  nil)

(defn- unregister-shutdown-hook!
  [dir]
  (when-let [^Thread hook (get @shutdown-hooks dir)]
    (swap! shutdown-hooks dissoc dir)
    (try
      (.removeShutdownHook (Runtime/getRuntime) hook)
      (catch IllegalStateException _)
      (catch IllegalArgumentException _)
      (catch SecurityException _)))
  nil)

(defn- stop-scheduled-sync
  [scheduled-sync]
  (when-let [fut @scheduled-sync]
    (.cancel ^ScheduledFuture fut true)
    (vreset! scheduled-sync nil)))

(defn- copy-version-file
  [lmdb dest]
  (let [src (str (env-dir lmdb) u/+separator+ c/version-file-name)
        dst (str dest u/+separator+ c/version-file-name)]
    (u/copy-file src dst)))

(defn- copy-keycode-file
  [lmdb dest]
  (let [src (str (env-dir lmdb) u/+separator+ c/keycode-file-name)
        dst (str dest u/+separator+ c/keycode-file-name)]
    (when (.exists (io/file src))
      (u/copy-file src dst))))

(declare key-range-list-count-fast key-range-list-count-slow)

(deftype CppLMDB [^Env env
                  info
                  ^ThreadLocal tl-reader
                  ^ConcurrentHashMap reader-registry
                  ^HashMap dbis
                  scheduled-sync
                  ^BufVal kp-w
                  ^BufVal vp-w
                  ^BufVal start-kp-w
                  ^BufVal stop-kp-w
                  ^BufVal start-vp-w
                  ^BufVal stop-vp-w
                  ^ByteBuffer k-comp-bf-w
                  ^:volatile-mutable ^ByteBuffer v-comp-bf-w
                  write-txn
                  writing?
                  ^:volatile-mutable k-comp
                  ^:volatile-mutable v-comp
                  ^:unsynchronized-mutable meta]

  IWriting
  (writing? [_] writing?)

  (write-txn [_] write-txn)

  (mark-write [_]
    (->CppLMDB
      env info tl-reader reader-registry dbis scheduled-sync kp-w vp-w start-kp-w
      stop-kp-w start-vp-w stop-vp-w k-comp-bf-w v-comp-bf-w
      write-txn true k-comp v-comp meta))

  (reset-write
    [this]
    (.clear kp-w)
    (.clear vp-w)
    (.clear start-kp-w)
    (.clear stop-kp-w)
    (.clear start-vp-w)
    (.clear stop-vp-w)
    (.clear k-comp-bf-w)
    (when-some [^ByteBuffer bf v-comp-bf-w]
      (.clear bf))
    (vreset! write-txn (Rtx. this
                             (Txn/create env)
                             (volatile! 1)
                             kp-w
                             vp-w
                             start-kp-w
                             stop-kp-w
                             start-vp-w
                             stop-vp-w
                             k-comp-bf-w
                             v-comp-bf-w
                             (volatile! false)
                             (AtomicBoolean.)
                             false)))

  IObj
  (withMeta [this m] (set! meta m) this)
  (meta [_] meta)

  ILMDB
  (max-val-size [_] (or (:max-val-size @info) c/*init-val-size*))

  (set-max-val-size [_ size]
    (set! v-comp-bf-w (bf/allocate-buffer size))
    (vswap! info assoc :max-val-size size :max-val-size-changed? true))

  (close-kv [this]
    (let [dir         (env-dir this)
          dir-key     (local-kv-handle-key (u/file dir) (@info :flags))
          close-error (volatile! nil)]
      (when-not (.isClosed env)
        (unregister-shutdown-hook! dir)
        (stop-scheduled-sync scheduled-sync)
        (close-reader-rtxs! reader-registry)
        (when-let [^Rtx wtxn @write-txn]
          (close-rtx-quiet! wtxn)
          (vreset! write-txn nil))
        (.remove tl-reader)
        (let [dir-prefix (str (.env-dir this) u/+separator+)
              indices    (->> @l/vector-indices
                              (keep (fn [[fname idx]]
                                      (when (and (string? fname)
                                                 (s/starts-with? fname dir-prefix))
                                        idx)))
                              vec)]
          ;; Close vector indices while LMDB env is still valid.
          (doseq [idx indices]
            (try
              (close-vecs idx)
              (catch Throwable e
                (when-not @close-error
                  (vreset! close-error e))))))
        (doseq [^DBI db (.values dbis)]
          (try
            (close-dbi-quiet! db)
            (catch Throwable e
              (when-not @close-error
                (vreset! close-error e)))))
        (.clear dbis)
        (close-bufval-quiet! kp-w)
        (close-bufval-quiet! vp-w)
        (close-bufval-quiet! start-kp-w)
        (close-bufval-quiet! stop-kp-w)
        (close-bufval-quiet! start-vp-w)
        (close-bufval-quiet! stop-vp-w)
        (clean-buffer-quiet! k-comp-bf-w)
        (clean-buffer-quiet! v-comp-bf-w)
        (locking write-txn
          (try
            (.sync env 1)
            (catch Throwable e
              (when-not @close-error
                (vreset! close-error e))))
          (try
            (.close env)
            (catch Throwable e
              (when-not @close-error
                (vreset! close-error e)))))
        (when (@info :temp?) (u/delete-files (@info :dir))))
      (when (.isClosed env)
        (release-local-kv-handle! dir-key)
        (swap! l/lmdb-dirs disj dir)
        (when (zero? (count @l/lmdb-dirs))
          (l/shutdown-last-lmdb-executors!)))
      (when-let [e @close-error]
        (throw e))
      nil))

  (closed-kv? [_] (.isClosed env))

  (check-ready [this]
    (when (.closed-kv? this)
      (raise "LMDB env is closed." {:type :lmdb/closed})))

  (env-dir [_] (@info :dir))
  (kv-info [_] info)

  (env-opts [_] (dissoc @info :dbis))

  (dbi-opts [_ dbi-name] (get-in @info [:dbis dbi-name]))

  (key-compressor [_] k-comp)

  (set-key-compressor [_ c] (set! k-comp c))

  (val-compressor [_] v-comp)

  (set-val-compressor [_ c] (set! v-comp c))

  (open-dbi [this dbi-name]
    (.open-dbi this dbi-name nil))
  (open-dbi [this dbi-name
             {:keys [key-size val-size flags validate-data?]
              :or   {key-size       (or (get-in @info [:dbis dbi-name :key-size])
                                        c/+max-key-size+)
                     val-size       (or (get-in @info [:dbis dbi-name :val-size])
                                        c/*init-val-size*)
                     flags          (or (get-in @info [:dbis dbi-name :flags])
                                        c/default-dbi-flags)
                     validate-data? (or (get-in @info
                                                [:dbis dbi-name :validate-data?])
                                        false)}}]
    (.check-ready this)
    (assert (< ^long key-size 512) "Key size cannot be greater than 511 bytes")
    (let [{info-dbis :dbis max-dbis :max-dbs} @info]
      (if (< (count info-dbis) ^long max-dbis)
        (let [existing-opts (get info-dbis dbi-name)
              opts     {:key-size       key-size
                        :val-size       val-size
                        :flags          flags
                        :validate-data? validate-data?}
              flags    (set flags)
              dupsort? (if (:dupsort flags) true false)
              counted? (if (:counted flags) true false)
              kp       (new-bufval key-size)
              vp       (new-bufval val-size)
              kc       (bf/allocate-buffer key-size)
              vc       (bf/allocate-buffer val-size)
              dbi      (Dbi/create env dbi-name (kv-flags flags))
              db       (DBI. this dbi (new-pools) kp vp kc vc
                             dupsort? counted? validate-data?)]
          (when (not= dbi-name c/kv-info)
            (when (not= existing-opts opts)
              (vswap! info assoc-in [:dbis dbi-name] opts)
              (transact-kv this [(l/kv-tx :put c/kv-info [:dbis dbi-name] opts
                                          [:keyword :string])])))
          (.put dbis dbi-name db)
          db)
        (u/raise (str "Reached maximal number of DBI: " max-dbis) {}))))

  (get-dbi [this dbi-name]
    (.get-dbi this dbi-name true))
  (get-dbi [this dbi-name create?]
    (or (.get dbis dbi-name)
        (if create?
          (.open-dbi this dbi-name)
          (u/raise (str "DBI " dbi-name " is not open") {}))))

  (clear-dbi [this dbi-name]
    (.check-ready this)
    (try
      (let [^Dbi dbi (.-db ^DBI (.get-dbi this dbi-name))
            ^Txn txn (Txn/create env)]
        (Util/checkRc (DTLV/mdb_drop (.get txn) (.get dbi) 0))
        (.commit txn))
      (catch Util$MapFullException _
        (let [^Info info (Info/create env)]
          (.setMapSize env (* ^long c/+buffer-grow-factor+
                              (.me_mapsize ^DTLV$MDB_envinfo (.get info))))
          (.close info))
        (.clear-dbi this dbi-name))
      (catch Exception e
        (raise "Fail to clear DBI: " dbi-name " " e {}))))

  (drop-dbi [this dbi-name]
    (.check-ready this)
    (try
      (let [^Dbi dbi (.-db ^DBI (.get-dbi this dbi-name))
            ^Txn txn (Txn/create env)]
        (Util/checkRc (DTLV/mdb_drop (.get txn) (.get dbi) 1))
        (.commit txn)
        (vswap! info update :dbis dissoc dbi-name)
        (transact-kv this c/kv-info
                     [[:del [:dbis dbi-name]]] [:keyword :string])
        (.remove dbis dbi-name)
        nil)
      (catch Exception e (raise "Fail to drop DBI: " dbi-name e {}))))

  (list-dbis [_] (keys (@info :dbis)))

  (copy [this dest]
    (.copy this dest false))
  (copy [this dest compact?]
    (if (-> dest u/file u/empty-dir?)
      (do (.copy env dest (if compact? true false))
          (copy-version-file this dest)
          (copy-keycode-file this dest))
      (raise "Destination directory is not empty." {})))

  (get-rtx [this]
    (when-not (.closed-kv? this)
      (try
        (or (reusable-reader-rtx this tl-reader reader-registry)
            (do
              (sweep-dead-reader-rtxs! reader-registry)
              (fresh-reader-rtx this env tl-reader reader-registry)))
        (catch Exception e
          (raise duplicate-local-open-msg
                 {:cause (.getMessage e)})))))

  (return-rtx [this rtx]
    (when-not (.closed-kv? this)
      (if (.isVirtual (Thread/currentThread))
        (try
          (close-rtx-quiet! rtx)
          (finally
            (.remove tl-reader)))
        (.reset ^Rtx rtx))))

  (stat [_]
    (try
      (let [stat ^Stat (Stat/create env)
            m    (stat-map stat)]
        (.close stat)
        m)
      (catch Exception e
        (raise "Fail to get statistics: " e {}))))
  (stat [this dbi-name]
    (if dbi-name
      (let [^Rtx rtx (.get-rtx this)]
        (try
          (let [^DBI dbi   (.get-dbi this dbi-name false)
                ^Dbi db    (.-db dbi)
                ^Txn txn   (.-txn rtx)
                ^Stat stat (Stat/create txn db)
                m          (stat-map stat)]
            (.close stat)
            m)
          (catch Exception e
            (raise "Fail to get statistics: " e {:dbi dbi-name}))
          (finally (.return-rtx this rtx))))
      (stat this)))

  (entries [this dbi-name]
    (let [^DBI dbi (.get-dbi this dbi-name)
          ^Rtx rtx (.get-rtx this)
          ^Dbi db  (.-db dbi)
          ^Txn txn (.-txn rtx)]
      (try
        (if (.-counted? dbi)
          (with-open [^LongPointer ptr (LongPointer. 1)]
            (DTLV/mdb_count_all (.get txn) (.get db) (int 0) ptr)
            (.get ptr))
          (let [^Stat stat (Stat/create txn db)
                entries    (.ms_entries ^DTLV$MDB_stat (.get stat))]
            (.close stat)
            entries))
        (catch Exception e
          (raise "Fail to get entries: " (ex-message e) {:dbi dbi-name}))
        (finally (.return-rtx this rtx)))))

  (open-transact-kv [this]
    (.check-ready this)
    (try
      (.reset-write this)
      (.mark-write this)
      (catch Exception e
        (raise "Fail to open read/write transaction in LMDB: " e {}))))

  (close-transact-kv [_]
    (if-let [^Rtx wtxn @write-txn]
      (when-let [^Txn txn (.-txn wtxn)]
        (let [aborted? @(.-aborted? wtxn)]
          (if aborted?
            (.close txn)
            (try
              (run-before-write-commit! {:operation :close-transact-kv})
              (.commit txn)
              (catch Util$MapFullException _
                (.close txn)
                (up-db-size env)
                (vreset! write-txn nil)
                (raise "DB resized" {:resized true}))
              (catch Exception e
                (.close txn)
                (vreset! write-txn nil)
                (if (= :ha/write-rejected (:error (ex-data e)))
                  (throw e)
                  (raise "Fail to commit read/write transaction in LMDB: "
                         e {})))))
          (vreset! write-txn nil)
          (.close txn)
          (if aborted? :aborted :committed)))
      (raise "Calling `close-transact-kv` without opening" {})))

  (abort-transact-kv [_]
    (when-let [^Rtx wtxn @write-txn]
      (vreset! (.-aborted? wtxn) true)
      (vreset! write-txn wtxn)
      nil))

  (transact-kv [this txs] (.transact-kv this nil txs))
  (transact-kv [this dbi-name txs]
    (.transact-kv this dbi-name txs :data :data))
  (transact-kv [this dbi-name txs k-type]
    (.transact-kv this dbi-name txs k-type :data))
  (transact-kv [this dbi-name txs k-type v-type]
    (let [^clojure.lang.IPersistentVector prepared-one-shot
          (let [tx-open? (some? @write-txn)]
            (when-not tx-open?
              (prepare-kvtx-ops txs dbis dbi-name k-type v-type)))]
      (letfn [(do-transact [prepared]
              (.check-ready this)
              (let [^Rtx rtx  @write-txn
                    one-shot? (nil? rtx)
                    ^DBI dbi  (when dbi-name
                                (or (.get dbis dbi-name)
                                    (raise dbi-name " is not open" {})))
                    ^Txn txn  (if one-shot?
                                (Txn/create env)
                                (.-txn rtx))]
                (try
                  (if prepared
                    (transact-prepared-ops* prepared txn)
                    (if dbi
                      (transact1* txs dbi txn k-type v-type)
                      (transact* txs dbis txn)))
                  (when (:max-val-size-changed? @info)
                    (transact* [[:put c/kv-info :max-val-size (:max-val-size @info)]]
                               dbis txn)
                    (vswap! info assoc :max-val-size-changed? false))
                  (when one-shot?
                    (run-before-write-commit! {:operation :transact-kv
                                               :dbi-name dbi-name})
                    (.commit txn))
                  :transacted
                  (catch Util$MapFullException _
                    (.close txn)
                    (up-db-size env)
                    (if one-shot?
                      (.transact-kv this dbi-name txs k-type v-type)
                      (do (.reset-write this)
                          (raise "DB resized" {:resized true}))))
                  (catch Exception e
                    (when one-shot? (.close txn))
                    (if (= :ha/write-rejected (:error (ex-data e)))
                      (throw e)
                      (raise "Fail to transact to LMDB: " e {}))))))]
        (if (Thread/holdsLock write-txn)
          (do-transact nil)
          (locking write-txn
            (do-transact prepared-one-shot))))))

  (set-env-flags [_ ks on-off] (.setFlags env (kv-flags ks) (if on-off 1 0)))

  (get-env-flags [_] (env-flag-keys (.getFlags env)))

  (sync [_] (.sync env 1))
  (sync [_ force] (.sync env force))

  (get-value [this dbi-name k]
    (.get-value this dbi-name k :data :data true))
  (get-value [this dbi-name k k-type]
    (.get-value this dbi-name k k-type :data true))
  (get-value [this dbi-name k k-type v-type]
    (.get-value this dbi-name k k-type v-type true))
  (get-value [this dbi-name k k-type v-type ignore-key?]
    (scan/get-value this dbi-name k k-type v-type ignore-key?))

  (get-rank [this dbi-name k]
    (.get-rank this dbi-name k :data))
  (get-rank [this dbi-name k k-type]
    (scan/get-rank this dbi-name k k-type))

  (get-by-rank [this dbi-name rank]
    (.get-by-rank this dbi-name rank :data :data true))
  (get-by-rank [this dbi-name rank k-type]
    (.get-by-rank this dbi-name rank k-type :data true))
  (get-by-rank [this dbi-name rank k-type v-type]
    (.get-by-rank this dbi-name rank k-type v-type true))
  (get-by-rank [this dbi-name rank k-type v-type ignore-key?]
    (scan/get-by-rank this dbi-name rank k-type v-type ignore-key?))

  (sample-kv [this dbi-name n]
    (.sample-kv this dbi-name n :data :data true))
  (sample-kv [this dbi-name n k-type]
    (.sample-kv this dbi-name n k-type :data true))
  (sample-kv [this dbi-name n k-type v-type]
    (.sample-kv this dbi-name n k-type v-type true))
  (sample-kv [this dbi-name n k-type v-type ignore-key?]
    (scan/sample-kv this dbi-name n k-type v-type ignore-key?))

  (get-first [this dbi-name k-range]
    (.get-first this dbi-name k-range :data :data false))
  (get-first [this dbi-name k-range k-type]
    (.get-first this dbi-name k-range k-type :data false))
  (get-first [this dbi-name k-range k-type v-type]
    (.get-first this dbi-name k-range k-type v-type false))
  (get-first [this dbi-name k-range k-type v-type ignore-key?]
    (scan/get-first this dbi-name k-range k-type v-type ignore-key?))

  (get-first-n [this dbi-name n k-range]
    (.get-first-n this dbi-name n k-range :data :data false))
  (get-first-n [this dbi-name n k-range k-type]
    (.get-first-n this dbi-name n k-range k-type :data false))
  (get-first-n [this dbi-name n k-range k-type v-type]
    (.get-first-n this dbi-name n k-range k-type v-type false))
  (get-first-n [this dbi-name n k-range k-type v-type ignore-key?]
    (scan/get-first-n this dbi-name n k-range k-type v-type ignore-key?))

  (get-range [this dbi-name k-range]
    (.get-range this dbi-name k-range :data :data false))
  (get-range [this dbi-name k-range k-type]
    (.get-range this dbi-name k-range k-type :data false))
  (get-range [this dbi-name k-range k-type v-type]
    (.get-range this dbi-name k-range k-type v-type false))
  (get-range [this dbi-name k-range k-type v-type ignore-key?]
    (scan/get-range this dbi-name k-range k-type v-type ignore-key?))

  (key-range [this dbi-name k-range]
    (.key-range this dbi-name k-range :data))
  (key-range [this dbi-name k-range k-type]
    (scan/key-range this dbi-name k-range k-type))

  (visit-key-range [this dbi-name visitor k-range]
    (.visit-key-range this dbi-name visitor k-range :data true))
  (visit-key-range [this dbi-name visitor k-range k-type]
    (.visit-key-range this dbi-name visitor k-range k-type true))
  (visit-key-range [this dbi-name visitor k-range k-type raw-pred?]
    (scan/visit-key-range this dbi-name visitor k-range k-type raw-pred?))

  (key-range-count [lmdb dbi-name k-range]
    (.key-range-count lmdb dbi-name k-range :data))
  (key-range-count [lmdb dbi-name [range-type k1 k2] k-type]
    (scan/scan
      (let [^RangeContext ctx (l/range-info rtx range-type k1 k2 k-type)
            forward?          (.-forward? ctx)
            lower             (if forward? (.-start-bf ctx) (.-stop-bf ctx))
            upper             (if forward? (.-stop-bf ctx) (.-start-bf ctx))
            include-lower?    (if forward? (.-include-start? ctx) (.-include-stop? ctx))
            include-upper?    (if forward? (.-include-stop? ctx) (.-include-start? ctx))
            flag              (BitOps/intOr
                                (if include-lower? (int DTLV/MDB_COUNT_LOWER_INCL) 0)
                                (if include-upper? (int DTLV/MDB_COUNT_UPPER_INCL) 0))]
        (with-open [total (LongPointer. 1)]
          (DTLV/mdb_range_count_keys
            (.get ^Txn (.-txn ^Rtx rtx)) (.get ^Dbi (.-db ^DBI dbi))
            (dtlv-val lower) (dtlv-val upper) flag total)
          (.get ^LongPointer total)))
      (raise "Fail to count key range: " e {:dbi dbi-name})))

  (range-seq [this dbi-name k-range]
    (.range-seq this dbi-name k-range :data :data false nil))
  (range-seq [this dbi-name k-range k-type]
    (.range-seq this dbi-name k-range k-type :data false nil))
  (range-seq [this dbi-name k-range k-type v-type]
    (.range-seq this dbi-name k-range k-type v-type false nil))
  (range-seq [this dbi-name k-range k-type v-type ignore-key?]
    (.range-seq this dbi-name k-range k-type v-type ignore-key? nil))
  (range-seq [this dbi-name k-range k-type v-type ignore-key? opts]
    (scan/range-seq this dbi-name k-range k-type v-type ignore-key? opts))

  (range-count [this dbi-name k-range]
    (.range-count this dbi-name k-range :data))
  (range-count [lmdb dbi-name k-range k-type]
    (let [dupsort? (.-dupsort? ^DBI (.get dbis dbi-name))]
      (if dupsort?
        (.list-range-count lmdb dbi-name k-range k-type)
        (.key-range-count lmdb dbi-name k-range k-type))))

  (get-some [this dbi-name pred k-range]
    (.get-some this dbi-name pred k-range :data :data false true))
  (get-some [this dbi-name pred k-range k-type]
    (.get-some this dbi-name pred k-range k-type :data false true))
  (get-some [this dbi-name pred k-range k-type v-type]
    (.get-some this dbi-name pred k-range k-type v-type false true))
  (get-some [this dbi-name pred k-range k-type v-type ignore-key?]
    (.get-some this dbi-name pred k-range k-type v-type ignore-key? true))
  (get-some [this dbi-name pred k-range k-type v-type ignore-key? raw-pred?]
    (scan/get-some this dbi-name pred k-range k-type v-type ignore-key?
                   raw-pred?))

  (range-filter [this dbi-name pred k-range]
    (.range-filter this dbi-name pred k-range :data :data false true))
  (range-filter [this dbi-name pred k-range k-type]
    (.range-filter this dbi-name pred k-range k-type :data false true))
  (range-filter [this dbi-name pred k-range k-type v-type]
    (.range-filter this dbi-name pred k-range k-type v-type false true))
  (range-filter [this dbi-name pred k-range k-type v-type ignore-key?]
    (.range-filter this dbi-name pred k-range k-type v-type ignore-key? true))
  (range-filter [this dbi-name pred k-range k-type v-type ignore-key? raw-pred?]
    (scan/range-filter this dbi-name pred k-range k-type v-type ignore-key?
                       raw-pred?))

  (range-keep [this dbi-name pred k-range]
    (.range-keep this dbi-name pred k-range :data :data true))
  (range-keep [this dbi-name pred k-range k-type]
    (.range-keep this dbi-name pred k-range k-type :data true))
  (range-keep [this dbi-name pred k-range k-type v-type]
    (.range-keep this dbi-name pred k-range k-type v-type true))
  (range-keep [this dbi-name pred k-range k-type v-type raw-pred?]
    (scan/range-keep this dbi-name pred k-range k-type v-type raw-pred?))

  (range-some [this dbi-name pred k-range]
    (.range-some this dbi-name pred k-range :data :data true))
  (range-some [this dbi-name pred k-range k-type]
    (.range-some this dbi-name pred k-range k-type :data true))
  (range-some [this dbi-name pred k-range k-type v-type]
    (.range-some this dbi-name pred k-range k-type v-type true))
  (range-some [this dbi-name pred k-range k-type v-type raw-pred?]
    (scan/range-some this dbi-name pred k-range k-type v-type raw-pred?))

  (range-filter-count [this dbi-name pred k-range]
    (.range-filter-count this dbi-name pred k-range :data :data true))
  (range-filter-count [this dbi-name pred k-range k-type]
    (.range-filter-count this dbi-name pred k-range k-type :data true))
  (range-filter-count [this dbi-name pred k-range k-type v-type]
    (.range-filter-count this dbi-name pred k-range k-type v-type true))
  (range-filter-count [this dbi-name pred k-range k-type v-type raw-pred?]
    (scan/range-filter-count this dbi-name pred k-range k-type v-type raw-pred?))

  (visit [this dbi-name visitor k-range]
    (.visit this dbi-name visitor k-range :data :data true))
  (visit [this dbi-name visitor k-range k-type]
    (.visit this dbi-name visitor k-range k-type :data true))
  (visit [this dbi-name visitor k-range k-type v-type]
    (.visit this dbi-name visitor k-range k-type v-type true))
  (visit [this dbi-name visitor k-range k-type v-type raw-pred?]
    (scan/visit this dbi-name visitor k-range k-type v-type raw-pred?))

  (visit-key-sample
    [db dbi-name indices visitor k-range k-type]
    (.visit-key-sample db dbi-name indices visitor k-range k-type true))
  (visit-key-sample
    [db dbi-name indices visitor k-range k-type raw-pred?]
    (scan/visit-key-sample db dbi-name indices visitor k-range k-type raw-pred?))

  (open-list-dbi [this dbi-name {:keys [key-size val-size flags]
                                 :or   {key-size c/+max-key-size+
                                        val-size c/+max-key-size+
                                        flags    c/default-dbi-flags}}]
    (.check-ready this)
    (assert (and (>= c/+max-key-size+ ^long key-size)
                 (>= c/+max-key-size+ ^long val-size))
            "Data size cannot be larger than 511 bytes")
    (.open-dbi this dbi-name
               {:key-size key-size :val-size val-size
                :flags    (conj flags :dupsort)}))
  (open-list-dbi [lmdb dbi-name]
    (.open-list-dbi lmdb dbi-name nil))

  IList
  (list-dbi? [this dbi-name]
    (get-in (.dbi-opts this dbi-name) [:flags :dupsort]))
  (put-list-items [this dbi-name k vs kt vt]
    (.transact-kv this [(l/kv-tx :put-list dbi-name k vs kt vt)]))

  (del-list-items [this dbi-name k kt]
    (.transact-kv this [(l/kv-tx :del dbi-name k kt)]))
  (del-list-items [this dbi-name k vs kt vt]
    (.transact-kv this [(l/kv-tx :del-list dbi-name k vs kt vt)]))

  (get-list [this dbi-name k kt vt]
    (scan/get-list this dbi-name k kt vt))

  (visit-list [this dbi-name visitor k kt]
    (.visit-list this dbi-name visitor k kt :data true))
  (visit-list [this dbi-name visitor k kt vt]
    (.visit-list this dbi-name visitor k kt vt true))
  (visit-list [this dbi-name visitor k kt vt raw-pred?]
    (scan/visit-list this dbi-name visitor k kt vt raw-pred?))

  (list-count [lmdb dbi-name k kt]
    (.check-ready lmdb)
    (if k
      (scan/scan
        (list-count* rtx cur k kt)
        (raise "Fail to count list: " e {:dbi dbi-name :k k}))
      0))

  (near-list [lmdb dbi-name k v kt vt]
    (.check-ready lmdb)
    (scan/scan
      (near-list* rtx cur k kt v vt)
      (raise "Fail to get an item that is near in a list: "
             e {:dbi dbi-name :k k :v v})))

  (in-list? [lmdb dbi-name k v kt vt]
    (.check-ready lmdb)
    (if (and k v)
      (scan/scan
        (in-list?* rtx cur k kt v vt)
        (raise "Fail to test if an item is in list: "
               e {:dbi dbi-name :k k :v v}))
      false))

  (key-range-list-count [lmdb dbi-name k-range k-type]
    (key-range-list-count-fast lmdb dbi-name k-range k-type))

  (list-range [this dbi-name k-range kt v-range vt]
    (scan/list-range this dbi-name k-range kt v-range vt))

  (list-range-count [lmdb dbi-name k-range k-type]
    (key-range-list-count-fast lmdb dbi-name k-range k-type))

  (list-range-first [this dbi-name k-range kt v-range vt]
    (scan/list-range-first this dbi-name k-range kt v-range vt))

  (list-range-first-n [this dbi-name n k-range kt v-range vt]
    (scan/list-range-first-n this dbi-name n k-range kt v-range vt))

  (list-range-filter [this dbi-name pred k-range kt v-range vt]
    (.list-range-filter this dbi-name pred k-range kt v-range vt true))
  (list-range-filter [this dbi-name pred k-range kt v-range vt raw-pred?]
    (scan/list-range-filter this dbi-name pred k-range kt v-range vt raw-pred?))

  (list-range-keep [this dbi-name pred k-range kt v-range vt]
    (.list-range-keep this dbi-name pred k-range kt v-range vt true))
  (list-range-keep [this dbi-name pred k-range kt v-range vt raw-pred?]
    (scan/list-range-keep this dbi-name pred k-range kt v-range vt raw-pred?))

  (list-range-some [this list-name pred k-range k-type v-range v-type]
    (.list-range-some this list-name pred k-range k-type v-range v-type
                      true))
  (list-range-some [this dbi-name pred k-range kt v-range vt raw-pred?]
    (scan/list-range-some this dbi-name pred k-range kt v-range vt raw-pred?))

  (list-range-filter-count
    [this list-name pred k-range k-type v-range v-type]
    (.list-range-filter-count this list-name pred k-range k-type v-range
                              v-type true))
  (list-range-filter-count
    [this dbi-name pred k-range kt v-range vt raw-pred?]
    (scan/list-range-filter-count this dbi-name pred k-range kt v-range
                                  vt raw-pred?))

  (visit-list-range
    [this list-name visitor k-range k-type v-range v-type]
    (.visit-list-range this list-name visitor k-range k-type v-range
                       v-type true))
  (visit-list-range
    [this dbi-name visitor k-range kt v-range vt raw-pred?]
    (scan/visit-list-range this dbi-name visitor k-range kt v-range
                           vt raw-pred?))

  (visit-list-key-range
    [this dbi-name visitor k-range k-type v-type]
    (.visit-list-key-range this dbi-name visitor k-range k-type
                           v-type true))
  (visit-list-key-range
    [this dbi-name visitor k-range k-type v-type raw-pred?]
    (scan/visit-list-key-range this dbi-name visitor k-range k-type
                               v-type raw-pred?))

  (visit-list-sample
    [this list-name indices visitor k-range k-type v-type]
    (.visit-list-sample this list-name indices visitor k-range k-type v-type true))
  (visit-list-sample
    [this dbi-name indices visitor k-range kt vt raw-pred?]
    (scan/visit-list-sample this dbi-name indices visitor k-range kt vt raw-pred?))

  IAdmin
  (re-index [this opts] (l/re-index* this opts)))

(defn invalidate-thread-reader!
  [lmdb]
  (when (instance? CppLMDB lmdb)
    (let [^CppLMDB lmdb lmdb
          ^ThreadLocal tl-reader (.-tl-reader lmdb)
          ^ConcurrentHashMap reader-registry (.-reader-registry lmdb)
          thread (Thread/currentThread)]
      (when-let [^Rtx rtx (.get tl-reader)]
        (.remove reader-registry thread rtx)
        (close-rtx-quiet! rtx)
        (.remove tl-reader))))
  nil)

(defn- key-range-list-count-fast
  [lmdb dbi-name [range-type k1 k2] k-type]
  (scan/scan
   (let [^RangeContext ctx (l/range-info rtx range-type k1 k2 k-type)
         forward? (.-forward? ctx)
          ;; mdb_range_count_values expects (lower, upper) in ascending order.
          ;; For forward ranges: start-bf=lower, stop-bf=upper.
          ;; For backward ranges: start-bf=upper, stop-bf=lower, so swap.
         lower (if forward? (.-start-bf ctx) (.-stop-bf ctx))
         upper (if forward? (.-stop-bf ctx) (.-start-bf ctx))
         include-lower? (if forward? (.-include-start? ctx) (.-include-stop? ctx))
         include-upper? (if forward? (.-include-stop? ctx) (.-include-start? ctx))
         flag (BitOps/intOr
               (if include-lower? (int DTLV/MDB_COUNT_LOWER_INCL) 0)
               (if include-upper? (int DTLV/MDB_COUNT_UPPER_INCL) 0))]
     (with-open [ptr (LongPointer. 1)]
       (DTLV/mdb_range_count_values
        (.get ^Txn (.-txn ^Rtx rtx)) (.get ^Dbi (.-db ^DBI dbi))
        (dtlv-val lower) (dtlv-val upper)
        flag ptr)
       (.get ^LongPointer ptr)))
   (raise "Fail to count list in key range: " e {:dbi dbi-name})))

(defn- raw-header-type
  [header]
  (case (short header)
    (-64 -63 -8) :long
    -15          :bigint
    -14          :bigdec
    -11          :float
    -10          :double
    -9           :instant
    -7           :uuid
    -6           :string
    -5           :keyword
    -4           :symbol
    -3           :boolean
    -2           :bytes
    nil))

(defn- decode-kv-info-buffer
  [^ByteBuffer bf fallback-type]
  (try
    (b/read-buffer (.rewind bf) :data)
    (catch Exception data-e
      (if fallback-type
        (b/read-buffer (.rewind bf) fallback-type)
        (throw data-e)))))

(defn- decode-kv-info-entry
  [kv]
  (let [^ByteBuffer kb (l/k kv)
        ^ByteBuffer vb (l/v kv)
        key-type       (b/read-buffer (.rewind kb) :byte)
        val-type       (b/read-buffer (.rewind vb) :byte)]
    (when (and (not= key-type c/type-hete-tuple)
               (not= val-type c/type-bytes))
      (let [k (decode-kv-info-buffer kb
                                     (when (= key-type c/type-keyword)
                                       :keyword))
            dbi-key? (and (vector? k)
                          (= 2 (count k))
                          (= :dbis (nth k 0)))]
        (when-not dbi-key?
          (try
            [k (decode-kv-info-buffer vb (raw-header-type val-type))]
            (catch Exception e
              (u/raise "Fail to decode kv-info entry"
                       e
                       {:key k
                        :key-type key-type
                        :raw-val-type val-type}))))))))

(defn- load-info-from-kv
  [^CppLMDB lmdb]
  (let [dbis (into {}
                   (map (fn [[[_ dbi-name] opts]] [dbi-name opts]))
                   (get-range lmdb c/kv-info
                              [:closed
                               [:dbis :db.value/sysMin]
                               [:dbis :db.value/sysMax]]
                              [:keyword :string]))
        info (into {}
                   (i/range-keep lmdb c/kv-info decode-kv-info-entry
                                 [:all] :raw :raw true))]
    (c/canonicalize-wal-opts
     (assoc info :dbis dbis))))

(defn- init-info
  [^CppLMDB lmdb new-info]
  (transact-kv lmdb c/kv-info (map (fn [[k v]] [:put k v]) new-info))
  (vreset! (.-info lmdb) (merge new-info (load-info-from-kv lmdb))))

(defn- open-kv*
  [dir dir-file db-file {:keys [mapsize max-readers flags max-dbs temp?
                                key-compress val-compress]
                         :or {max-readers c/*max-readers*
                              max-dbs c/*max-dbs*
                              mapsize c/*init-db-size*
                              flags c/default-env-flags
                              temp? false}
                         :as opts}]
  (let [flags            (cond-> flags
                           temp? (conj :nosync))
        local-handle-key (reserve-local-kv-handle! dir-file flags)]
    (try
      (let [inmemory? (some #{:inmemory} flags)
          ;; MDB_INMEMORY on Windows expects a simple env identifier instead of
          ;; a filesystem path (which may include ':' or '\').
          env-path (if (and inmemory? (u/windows?))
                     (str "datalevin-inmemory-" (java.util.UUID/randomUUID))
                     dir)
          mapsize (* (long (if (or inmemory? (not (.exists ^File db-file)))
                             mapsize
                             (c/pick-mapsize db-file)))
                     1024 1024)
          flags (cond-> flags
                  inmemory? (conj :nosync))
          ^Env env (Env/create env-path mapsize max-readers max-dbs
                               (kv-flags flags))
          info (cond-> (merge opts {:dir dir
                                    :flags flags
                                    :max-readers max-readers
                                    :max-dbs max-dbs
                                    :temp? temp?})
                 key-compress (assoc :key-compress key-compress)
                 val-compress (assoc :val-compress val-compress))
          ^CppLMDB lmdb (->CppLMDB env
                                   (volatile! info)
                                   (ThreadLocal.)
                                   (ConcurrentHashMap.)
                                   (HashMap.)
                                   (volatile! nil)
                                   (new-bufval c/+max-key-size+)
                                   (new-bufval 0)
                                   (new-bufval c/+max-key-size+)
                                   (new-bufval c/+max-key-size+)
                                   (new-bufval c/+max-key-size+)
                                   (new-bufval c/+max-key-size+)
                                   (bf/allocate-buffer c/+max-key-size+)
                                   nil
                                   (volatile! nil)
                                   false
                                   nil
                                   nil
                                   nil)]
        (swap! l/lmdb-dirs conj dir)
        (open-dbi lmdb c/kv-info) ;; never compressed
        (cond
          inmemory? nil
          temp? (u/delete-on-exit dir-file)
          :else
          (let [k-comp (when (and key-compress
                                  (.exists (io/file dir c/keycode-file-name)))
                         (cp/load-key-compressor
                          (str dir u/+separator+ c/keycode-file-name)))
                v-comp (when (and val-compress
                                  (.exists (io/file dir c/valcode-file-name)))
                         (cp/load-val-compressor
                          (str dir u/+separator+ c/valcode-file-name)))
                loaded-info (load-info-from-kv lmdb)]
            (if (empty? loaded-info)
              (init-info lmdb info)
              (vreset! (.-info lmdb)
                       (assoc (merge loaded-info info)
                              :dbis (:dbis loaded-info))))
            (set-max-val-size lmdb (max-val-size lmdb))
            (set-key-compressor lmdb k-comp)
            (set-val-compressor lmdb v-comp)
            (register-shutdown-hook! dir (Thread. #(close-kv lmdb)))
            (start-scheduled-sync (.-scheduled-sync lmdb) dir env)))
        (register-local-kv-handle! local-handle-key (l/wrap-open-kv lmdb)))
      (catch Exception e
        (release-local-kv-handle! local-handle-key)
        (raise "Fail to open database: " e {:dir dir})))))

(defmethod open-kv :cpp
  ([dir] (open-kv dir {}))
  ([dir opts]
   (let [opts (c/canonicalize-wal-opts opts)
         inmemory? (or (nil? dir)
                       (:inmemory? opts)
                       (some #{:inmemory} (:flags opts)))
         dir (or dir (str (u/tmp-dir) (java.util.UUID/randomUUID)))
         _ (assert (string? dir) "directory should be a string.")
         dir-file (u/file dir)
         db-file (io/file dir c/data-file-name)
         opts (if inmemory?
                (update opts :flags (fnil conj c/default-env-flags) :inmemory)
                opts)]
     (if inmemory?
       (open-kv* dir dir-file db-file opts)
       (let [exist-db? (.exists db-file)
             version (read-version-file dir-file)]
         (cond
           (not exist-db?)
           (let [lmdb (open-kv* dir dir-file db-file opts)]
             (write-version-file dir-file c/version)
             lmdb)
           version
           (do
             (when (not= version c/version)
               (if-let [{:keys [major minor patch]} (c/parse-version version)]
                 (let [{cmajor :major cminor :minor} (c/parse-version c/version)]
                   (when (and c/require-migration?
                              (or (< ^long major ^long cmajor)
                                  (< ^long minor ^long cminor)))
                     (m/perform-migration dir major minor patch))
                   (write-version-file dir-file c/version))
                 (raise "Corrupt VERSION file" {:input version})))
             (open-kv* dir dir-file db-file opts))
           :else
           (raise "Database requires migration. Please follow instruction at https://github.com/datalevin/datalevin/blob/master/doc/upgrade.md"
                  {:dir dir})))))))
