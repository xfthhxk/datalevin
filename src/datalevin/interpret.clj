;;
;; Copyright (c) Huahai Yang. All rights reserved.
;; The use and distribution terms for this software are covered by the
;; Eclipse Public License 2.0 (https://opensource.org/license/epl-2-0)
;; which can be found in the file LICENSE at the root of this distribution.
;; By using this software in any fashion, you are agreeing to be bound by
;; the terms of this license.
;; You must not remove this notice, or any other, from this software.
;;
(ns datalevin.interpret
  "Code interpreter, including functions and macros useful for command line
  and query/transaction functions."
  (:require
   [clojure.walk :as w]
   [clojure.set :as set]
   [clojure.pprint :as p]
   [clojure.java.io :as io]
   [sci.core :as sci]
   [taoensso.nippy :as nippy]
   [datalevin.query-util :as qu]
   [datalevin.util :as u]
   [datalevin.core]
   [datalevin.analyzer]
   [datalevin.stem]
   [datalevin.client]
   [datalevin.constants]
   [clojure.string :as s])
  (:import
   [clojure.lang AFn]
   [java.io DataInput DataOutput Writer]))

(def ^:no-doc user-facing-ns
  #{'datalevin.core 'datalevin.client 'datalevin.interpret
    'datalevin.constants})

(def ^:no-doc additional-ns
  #{'datalevin.analyzer})

(defn- user-facing? [v]
  (let [m (meta v)
        d (m :doc)]
    (and d
         (if-let [p (:protocol m)]
           (and (not (:no-doc (meta p)))
                (not (:no-doc m)))
           (not (:no-doc m)))
         (not (s/starts-with? d "Positional factory function for class")))))

(defn ^:no-doc available-map [ns var-map pred]
  (let [sci-ns (sci/create-ns ns)]
    (reduce
      (fn [m [k v]]
        (assoc m k (sci/new-var (symbol v) v (assoc (meta v)
                                                    :sci.impl/built-in true
                                                    :ns sci-ns))))
      {}
      (select-keys var-map (keep (fn [[k v]] (when (pred v) k)) var-map)))))

(defn ^:no-doc user-facing-map [ns var-map]
  (available-map ns var-map user-facing?))

(defn ^:no-doc additional-map [ns var-map]
  (available-map ns var-map (constantly true)))

(defn- user-facing-vars []
  (reduce
    (fn [m ns]
      (assoc m ns (user-facing-map ns (ns-publics ns))))
    {}
    user-facing-ns))

(defn- additional-vars []
  (reduce
    (fn [m ns]
      (assoc m ns (additional-map ns (ns-publics ns))))
    {}
    additional-ns))

(defn ^:no-doc resolve-var [s]
  (when (symbol? s)
    (some #(ns-resolve % s) (conj user-facing-ns *ns*))))

(defn- qualify-static-class-call
  [f]
  (when-let [class-part (and (symbol? f) (namespace f))]
    (let [resolved (resolve (symbol class-part))]
      (when (instance? Class resolved)
        (symbol (.getName ^Class resolved) (name f))))))

(defn- qualify-fn [x]
  (if (list? x)
    (let [[f & args] x]
      (cond
        (qu/rule-head f)
        x

        :else
        (if-let [var (resolve-var f)]
          (apply list (symbol var) args)
          (if-let [class-call (qualify-static-class-call f)]
            (apply list class-call args)
            x))))
    x))

(declare ctx inter-fn-ctx)

(defn ^:no-doc eval-fn [ctx form]
  (sci/eval-form ctx (if (coll? form)
                       (w/postwalk qualify-fn form)
                       form)))

(defn load-edn
  "Same as [`clojure.core/load-file`](https://clojuredocs.org/clojure.core/load-file),
   useful for e.g. loading schema from a file"
  [f]
  (let [f (io/file f)
        s (slurp f)]
    (sci/with-bindings {sci/ns   @sci/ns
                        sci/file (.getAbsolutePath f)}
      (sci/eval-string* ctx s))))

(defn exec-code
  "Execute code and print results. `code` is a string. Acceptable code includes
  Datalevin functions and some Clojure core functions."
  [code]
  (let [reader (sci/reader code)]
    (sci/with-bindings {sci/ns @sci/ns}
      (loop []
        (let [next-form (sci/parse-next ctx reader)]
          (when-not (= ::sci/eof next-form)
            (prn (eval-fn ctx next-form))
            (recur)))))))

;; inter-fn

(def ^:private disallowed-inter-fn-core-symbols
  '#{agent
     alias
     all-ns
     await
     await-for
     bean
     alter-var-root
     binding
     bound-fn
     bound-fn*
     create-ns
     declare
     def
     defmacro
     defmethod
     defmulti
     defn
     defonce
     defrecord
     deftype
     deftype*
     extend
     extend-protocol
     extend-type
     eval
     find-ns
     find-var
     future
     future-call
     get-thread-bindings
     import
     import*
     in-ns
     intern
     load
     load-file
     load-reader
     load-string
     loaded-libs
     locking
     macroexpand
     macroexpand-1
     memfn
     ns
     ns-aliases
     ns-imports
     ns-interns
     ns-map
     ns-publics
     ns-refers
     ns-resolve
     ns-unalias
     ns-unmap
     pcalls
     pmap
     promise
     proxy
     pvalues
     push-thread-bindings
     read
     read-line
     read-string
     refer
     reify
     reify*
     remove-ns
     require
     requiring-resolve
     resolve
     set!
     send
     send-off
     shutdown-agents
     slurp
     spit
     the-ns
     use
     var
     var-get
     var-set
     with-bindings
     with-bindings*
     with-open})

(def ^:private disallowed-inter-fn-core-symbol-set
  (into disallowed-inter-fn-core-symbols
        (map #(symbol "clojure.core" (name %)))
        disallowed-inter-fn-core-symbols))

(def ^:private allowed-inter-fn-core-api
  '#{add
     cardinality
     count-datoms
     datom
     datom-a
     datom-e
     datom-v
     datom?
     datoms
     db?
     entid
     entity
     entity-db
     explain
     max-eid
     pull
     pull-many
     q
     resolve-tempid
     retract
     rseek-datoms
     schema
     seek-datoms
     squuid
     squuid-time-millis
     tempid
     touch
     tx-data->simulated-report})

(def ^:private allowed-inter-fn-datalevin-namespaces
  #{"datalevin.analyzer"
    "datalevin.constants"
    "datalevin.core"})

(declare quote-form?)

(defn- class-like-namespace?
  [ns-part]
  (boolean
   (when ns-part
     (or (s/starts-with? ns-part "java.")
         (s/starts-with? ns-part "javax.")
         (s/starts-with? ns-part "jdk.")
         (s/starts-with? ns-part "sun.")
         (s/starts-with? ns-part "com.sun.")
         (some #(and (seq %)
                     (Character/isUpperCase ^char (first %)))
               (s/split ns-part #"\."))))))

(defn- interop-symbol?
  [sym]
  (let [n  (name sym)
        ns (namespace sym)]
    (or (= "." n)
        (= "new" n)
        (s/starts-with? n ".")
        (s/ends-with? n ".")
        (class-like-namespace? ns))))

(defn- disallowed-datalevin-symbol?
  [sym]
  (when-let [ns (namespace sym)]
    (or (and (= ns "datalevin.core")
             (not (contains? allowed-inter-fn-core-api
                             (symbol (name sym)))))
        (and (s/starts-with? ns "datalevin.")
             (not (contains? allowed-inter-fn-datalevin-namespaces ns))))))

(defn- quoted-query-dot-syntax?
  [quoted? sym]
  (and quoted?
       (nil? (namespace sym))
       (#{"." "..."} (name sym))))

(defn- validate-inter-fn-symbol!
  [quoted? sym]
  (when (or (contains? disallowed-inter-fn-core-symbol-set sym)
            (disallowed-datalevin-symbol? sym)
            (and (not (quoted-query-dot-syntax? quoted? sym))
                 (interop-symbol? sym)))
    (u/raise "Disallowed inter-fn symbol " sym
             {:type   :datalevin/disallowed-inter-fn-symbol
              :symbol sym})))

(defn- validate-inter-fn-code!
  ([form]
   (validate-inter-fn-code! false form))
  ([quoted? form]
   (cond
     (symbol? form)
     (validate-inter-fn-symbol! quoted? form)

     (seq? form)
     (if (quote-form? form)
       (validate-inter-fn-code! true (second form))
       (doseq [x form]
         (validate-inter-fn-code! quoted? x)))

     (map? form)
     (doseq [[k v] form]
       (validate-inter-fn-code! quoted? k)
       (validate-inter-fn-code! quoted? v))

     (coll? form)
     (doseq [x form]
       (validate-inter-fn-code! quoted? x))

     :else nil)))

(defn- qualify-code
  [form]
  (if (quote-form? form)
    form
    (w/walk qualify-code qualify-fn form)))

(defn- filter-used
  "Only keep referred locals in the form"
  [locals [args & body]]
  (let [args (set args)
        used (reduce (fn [coll s]
                       (if-not (or (qualified-symbol? s) (args s))
                         (conj coll s)
                         coll))
                     #{}
                     (filter symbol? (flatten body)))]
    (set/intersection (set locals) used)))

(defn- save-env
  "Borrowed some pieces from https://github.com/technomancy/serializable-fn"
  [locals form]
  (let [form        (cons 'fn (qualify-code (rest form)))
        quoted-form `(quote ~form)]
    (if locals
      `(list `let [~@(for [local   (filter-used locals (rest form)),
                           let-arg [`(quote ~local)
                                    `(list `quote ~local)]]
                       let-arg)]
             ~quoted-form)
      quoted-form)))

(defn- quote-form?
  [x]
  (and (seq? x)
       (= 'quote (first x))
       (= 2 (count x))))

(defn- fn-source-form?
  [x]
  (and (seq? x)
       (#{'fn 'clojure.core/fn} (first x))
       (or (vector? (second x))
           (and (symbol? (second x))
                (vector? (nth x 2 nil))))))

(defn- literal-let-source-form?
  [x]
  (and (seq? x)
       (#{'let 'clojure.core/let} (first x))
       (= 3 (count x))
       (let [bindings (second x)]
         (and (vector? bindings)
              (even? (count bindings))
              (every?
               (fn [[sym value]]
                 (and (symbol? sym)
                      (quote-form? value)))
               (partition 2 bindings))))
       (fn-source-form? (nth x 2 nil))))

(defn- inter-fn-source-form?
  [x]
  (or (fn-source-form? x)
      (literal-let-source-form? x)))

(defn ^:no-doc validate-inter-fn-source!
  [src]
  (when-not (inter-fn-source-form? src)
    (u/raise "Invalid inter-fn source form"
             {:type :datalevin/invalid-inter-fn-source
              :source src}))
  (validate-inter-fn-code! src)
  src)

(defn ^:no-doc compile-inter-fn-source
  "Compile validated inter-fn source with the restricted inter-fn interpreter."
  [src]
  (let [src (validate-inter-fn-source! src)]
    (with-meta
      (sci/eval-form inter-fn-ctx src)
      {:type   :datalevin/inter-fn
       :source src})))

(defn- source->inter-fn
  "Convert a source form to get an inter-fn"
  [src]
  (compile-inter-fn-source src))

(defmacro inter-fn
  "Same signature as `fn`. Create a function that can be serialized in
  source code form.

  Such a function can be used as an input in Datalevin queries or
  transactions, e.g. as a filtering predicate or as a transaction
  function, and be stored in the database. This function can also be
  sent over the wire if the database is on a remote server or as a
  babashka pod. It runs in an interpreter.

  Symbols referred in inter-fn needs to be fully-qualified."
  [_args & _body]
  `(compile-inter-fn-source ~(save-env (keys &env) &form)))

(defn inter-fn?
  "Return true if `x` is an `inter-fn`"
  [x]
  (= (:type (meta x)) :datalevin/inter-fn))

(defmacro definterfn
  "Create a named `inter-fn`"
  [fn-name args & body]
  `(def ~fn-name (inter-fn ~args ~@body)))

(nippy/extend-freeze AFn :datalevin/inter-fn
    [^AFn x ^DataOutput out]
  (if (inter-fn? x)
    (nippy/freeze-to-out! out (:source (meta x)))
    (u/raise "Can only freeze an inter-fn" {:x x})))

(nippy/extend-thaw :datalevin/inter-fn
    [^DataInput in]
  (let [src (nippy/thaw-from-in! in)]
    (source->inter-fn src)))

(defmethod print-method :datalevin/inter-fn [f, ^Writer w]
  (.write w "#datalevin/inter-fn ")
  (binding [*out* w] (p/pprint (:source (meta f)))))

(defn inter-fn-from-reader
  "Read a printed `inter-fn` back in."
  [x]
  (source->inter-fn x))

(def ^:no-doc sci-opts
  {:namespaces (merge (user-facing-vars) (additional-vars))
   :classes    {:allow                     :all
                'Thread                    java.lang.Thread
                'java.lang.Thread          java.lang.Thread
                'java.text.Normalizer      java.text.Normalizer
                'java.text.Normalizer$Form java.text.Normalizer$Form
                'org.tartarus.snowball.SnowballStemmer
                org.tartarus.snowball.SnowballStemmer}})

(defn- selected-vars-map
  [ns syms]
  (available-map ns (select-keys (ns-publics ns) syms) (constantly true)))

(defn- inter-fn-namespaces
  []
  {'clojure.core
   (zipmap disallowed-inter-fn-core-symbols
           (repeat nil))
   'datalevin.analyzer
   (additional-map 'datalevin.analyzer
                   (ns-publics 'datalevin.analyzer))
   'datalevin.constants
   (user-facing-map 'datalevin.constants
                    (ns-publics 'datalevin.constants))
   'datalevin.core
   (selected-vars-map 'datalevin.core allowed-inter-fn-core-api)})

(def ^:no-doc inter-fn-sci-opts
  {:namespaces (inter-fn-namespaces)
   :classes    {}})

(def ^:no-doc ctx (sci/init sci-opts))

(def ^:no-doc inter-fn-ctx (sci/init inter-fn-sci-opts))
